## Author : Aditya Shakya (adi1090x)
## Mail : adi1090x@gmail.com
## Github : @adi1090x
## Reddit : @adi1090x

// Screen size
screen.w = Window.GetWidth(0);
screen.h = Window.GetHeight(0);
screen.half.w = Window.GetWidth(0) / 2;
screen.half.h = Window.GetHeight(0) / 2;

// Question prompt
question = null;
answer = null;

// Message
message = null;

// Password prompt
bullets = null;
prompt = null;
// to create, run
// convert -font Comic\ Sans\ MS.ttf -pointsize 24 -fill "#FFFFFF" -background "#000000" label:"*" bullet.png
bullet.image = Image("bullet.png");

// Flow
state.status = "play";
state.time = 0.0;

// Animation starts before render, so lets start somewhere in the end
animation.cur_idx = 170;
animation.cur_sprite = Sprite();
animation.images_count = 198;

frames = 0;

//--------------------------------- Refresh (Logo animation) --------------------------

# cycle through all images
for (i = 1; i < animation.images_count; i++)
  raccoon_images[i] = Image("animation-" + i + ".png");
raccoon_sprite = Sprite();

fun refresh_callback ()
  {
    frames++;

    if (frames % 2 == 0) {
        return;
    }

    animation.cur_idx++;
    animation.cur_idx %= animation.images_count;

    raccoon_sprite.SetImage(raccoon_images[animation.cur_idx]);
    raccoon_sprite.SetPosition(Window.GetWidth() / 2 - raccoon_sprite.GetImage().GetWidth() / 2, Window.GetHeight() / 2 - raccoon_sprite.GetImage().GetHeight() / 2, 0);
    raccoon_sprite.SetOpacity(1);
  }

Plymouth.SetRefreshFunction (refresh_callback);

//------------------------------------- Password prompt -------------------------------
fun DisplayQuestionCallback(prompt, entry) {
    question = null;
    answer = null;

    if (entry == "")
        entry = "<answer>";

    question.image = Image.Text(prompt, 1, 1, 1);
    question.sprite = Sprite(question.image);
    question.sprite.SetX(screen.half.w - question.image.GetWidth() / 2);
    question.sprite.SetY(screen.h - 4 * question.image.GetHeight());

    answer.image = Image.Text(entry, 1, 1, 1);
    answer.sprite = Sprite(answer.image);
    answer.sprite.SetX(screen.half.w - answer.image.GetWidth() / 2);
    answer.sprite.SetY(screen.h - 2 * answer.image.GetHeight());
}
Plymouth.SetDisplayQuestionFunction(DisplayQuestionCallback);

//------------------------------------- Password prompt -------------------------------
fun DisplayPasswordCallback(nil, bulletCount) {
    state.status = "pause";
    totalWidth = bulletCount * bullet.image.GetWidth();
    startPos = screen.half.w - totalWidth / 2;

    // to create, run
    // convert -font Comic\ Sans\ MS.ttf -pointsize 24 -fill "#FFFFFF" -background "#000000" label:"Enter decryption password and tap YubiKey to continue" output.png
    prompt.image = Image("password_banner.png");
    prompt.sprite = Sprite(prompt.image);
    prompt.sprite.SetX(screen.half.w - prompt.image.GetWidth() / 2);
    prompt.sprite.SetY(screen.h / 2 + 6 * prompt.image.GetHeight());

    // Clear all bullets (user might hit backspace)
    bullets = null;
    for (i = 0; i < bulletCount; i++) {
        bullets[i].sprite = Sprite(bullet.image);
        bullets[i].sprite.SetX(startPos + i * bullet.image.GetWidth());
        bullets[i].sprite.SetY(screen.h / 2 + 7.5 * bullet.image.GetHeight());
    }
}
Plymouth.SetDisplayPasswordFunction(DisplayPasswordCallback);

//--------------------------- Normal display (unset all text) ----------------------
fun DisplayNormalCallback() {
    state.status = "play";
    bullets = null;
    prompt = null;
    message = null;
    question = null;
    answer = null;
}
Plymouth.SetDisplayNormalFunction(DisplayNormalCallback);

//----------------------------------------- Message --------------------------------
fun MessageCallback(text) {
    message.image = Image.Text(text, 1, 1, 1);
    message.sprite = Sprite(message.image);
    message.sprite.SetPosition(screen.half.w - message.image.GetWidth() / 2, message.image.GetHeight());
}
Plymouth.SetMessageFunction(MessageCallback);

